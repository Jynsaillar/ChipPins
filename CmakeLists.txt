cmake_minimum_required(VERSION 3.14)
project(
    ChipPins 
    VERSION 0.0.1
    DESCRIPTION "A library for defining pins in an embedded software context.")

set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

if(WIN32)

set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS YES CACHE BOOL "Export all symbols")

endif()

## C++ language configuration boilerplate

if (NOT DEFINED CMAKE_CXX_VISIBILITY_PRESET AND
    NOT DEFINED CMAKE_VISIBILITY_INLINES_HIDDEN)
  set(CMAKE_CXX_VISIBILITY_PRESET hidden)
  set(CMAKE_VISIBILITY_INLINES_HIDDEN YES)
endif ()

## Let ChipPins_SHARED_LIBS override BUILD_SHARED_LIBS

if (DEFINED ChipPins_SHARED_LIBS)
    set(BUILD_SHARED_LIBS "${ChipPins_SHARED_LIBS}")
endif ()

## Create the main ChipPins library target

add_library(ChipPins src/ChipPins.cpp)
add_library(ChipPins::ChipPins ALIAS ChipPins)
set_target_properties(ChipPins PROPERTIES
                      VERSION ${ChipPins_VERSION}
                      SOVERSION ${ChipPins_VERSION_MAJOR})
target_include_directories(
    ChipPins PUBLIC "$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>")
target_compile_features(ChipPins PUBLIC cxx_std_14)

## Generate the export header for ChipPins and attach it to the target

include(GenerateExportHeader)
generate_export_header(ChipPins EXPORT_FILE_NAME include/ChipPins/export.h)
target_compile_definitions(
    ChipPins PUBLIC "$<$<NOT:$<BOOL:${BUILD_SHARED_LIBS}>>:ChipPins_STATIC_DEFINE>")
target_include_directories(
    ChipPins PUBLIC "$<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/include>")

## Include the install rules if the user wanted them (included by default when top-level)

string(COMPARE EQUAL "${CMAKE_SOURCE_DIR}" "${CMAKE_CURRENT_SOURCE_DIR}" is_top_level)
option(ChipPins_INCLUDE_PACKAGING "Include packaging rules for ChipPins" "${is_top_level}")
if (ChipPins_INCLUDE_PACKAGING)
    add_subdirectory(packaging)
endif ()
